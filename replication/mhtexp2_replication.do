clear all
// replace path below with path to Karlan and List (2007)'s merged replication file
use "/location/of/AER merged.dta"

sort amount ask1 control ratio sizeno female askd1 cases ratio3 size100 ltmedmra close25 freq ask2 treatment size years redcty askd2 nonlit size25 mrm2 red0 amountchange hpa ask3 ask gave couple bluecty askd3 ratio2 size50 dormant blue0

gen newid = _n

merge 1:1 newid using data_id, nogenerate

sort rowid


//generate outcome
gen amountmat = amount * (1+ratio)
gen groupid = (redcty==1 & red0 == 1) + (redcty==0 & red0 == 1)*2 + (redcty == 0 & red0 == 0)*3 + (redcty==1 & red0 == 0)*4
replace groupid = . if groupid == 0

mata: mata mlib index


// drop observations that don't have data for controls
local allvars female pwhite pblack page18_39 ave_hh_sz years couple dormant nonlit cases groupid
foreach v in `allvars'{
                drop if `v' == .

}

disp _N
local sN = _N
disp `sN'

// explicitly setting options (these are already the default options)
local B = 3000 // number of bootstrap samples
local stu = 1 // studentization yes

// pregenerating the bootstrap matrix, to save time - this is the same seed and matrix generated by default options
mata: rseed(0)
mata: idbootmat = runiformint(`sN', `B', 1, `sN')

// multiple outcomes
mhtexp2 gave amount amountmat amountchange, treatment(treatment) studentized(`stu') bootstrap(`B') idbootmat(idbootmat)
mhtexp2 gave amount amountmat amountchange, treatment(treatment) controls(female pwhite pblack page18_39 ave_hh_sz years couple dormant nonlit cases) studentized(`stu') bootstrap(`B') idbootmat(idbootmat)

// multiple subgroups
mhtexp2 gave, treatment(treatment) subgroup(groupid) studentized(`stu') bootstrap(`B') idbootmat(idbootmat)
mhtexp2 gave, treatment(treatment) subgroup(groupid) controls(female pwhite pblack page18_39 ave_hh_sz years couple dormant nonlit cases) studentized(`stu') bootstrap(`B') idbootmat(idbootmat)

// multiple treatments
mhtexp2 amount, treatment(ratio) studentized(`stu') bootstrap(`B') idbootmat(idbootmat)
mhtexp2 amount, treatment(ratio) controls(female pwhite pblack page18_39 ave_hh_sz years couple dormant nonlit cases) studentized(`stu') bootstrap(`B') idbootmat(idbootmat)

// multiple treatments, with pairwise comparisons
mhtexp2 amount, treatment(ratio) combo("pairwise") studentized(`stu') bootstrap(`B') idbootmat(idbootmat)
mhtexp2 amount, treatment(ratio) combo("pairwise") controls(female pwhite pblack page18_39 ave_hh_sz years couple dormant nonlit cases) studentized(`stu') bootstrap(`B') idbootmat(idbootmat)


// multiple outcomes, subgroups, and treatments
mhtexp2 gave amount amountmat amountchange, subgroup(groupid) treatment(ratio) studentized(`stu') bootstrap(`B') idbootmat(idbootmat)
mhtexp2 gave amount amountmat amountchange, subgroup(groupid) treatment(ratio) controls(female pwhite pblack page18_39 ave_hh_sz years couple dormant nonlit cases) studentized(`stu') bootstrap(`B') idbootmat(idbootmat)
